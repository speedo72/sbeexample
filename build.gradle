plugins {
    id 'java-library' apply true
}

repositories {
    mavenCentral()
}

configurations {
    codecGeneration
}

dependencies {
    api 'uk.co.real-logic:sbe-tool:1.27.0'
    codecGeneration 'uk.co.real-logic:sbe-tool:1.27.0'
    api files('build/classes/java/generated')
    implementation 'io.aeron:aeron-all:1.40.0'
}

def generatedDir = file("${buildDir}/generated-src")
sourceSets {
    generated {
        java.srcDir generatedDir
        compileClasspath += configurations.codecGeneration
    }
}

compileJava.dependsOn 'compileGeneratedJava'
compileGeneratedJava.dependsOn 'generateCodecs'

task generateCodecs(type: JavaExec) {
    mainClass.set('uk.co.real_logic.sbe.SbeTool')
    classpath = configurations.codecGeneration
    systemProperties(
            'sbe.xinclude.aware': 'true',
            'sbe.validation.xsd': projectDir.toString() + '/src/main/resources/fpl/sbe.xsd',
            'sbe.validation.stop.on.error': 'true',
            'sbe.validation.warnings.fatal': 'true',
            'sbe.generate.stubs': 'true',
            'sbe.target.language': 'java',
            'sbe.output.dir': 'build/generated-src',
            'sbe.java.generate.interfaces': 'false',
            'sbe.target.namespace': 'schema')
    args = ['src/main/resources/schema.xml']
}